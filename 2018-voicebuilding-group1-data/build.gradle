plugins {
	id 'base'
	id "de.dfki.mary.voicebuilding.marytts-kaldi-mfa" version "0.3.0"
}

group 'de.uni-sb.coli.voicebuilding'
version '0.1-SNAPSHOT'

task process(type: Exec){
	inputs.files "process.py", "Annotations.Table", "cmuarctic.data.txt"
	ext.metadataFile = "$buildDir/metaData.txt"
	outputs.file metadataFile
	commandLine "python", "process.py", "Annotations.Table", "cmuarctic.data.txt"
	doFirst {
		buildDir.mkdirs()
	}
}

task extract(type: Exec){
	dependsOn process
	def pyFile = "extract.py"
	def flacFile = "2018-voicebuilding-group1-raw-audio.flac"
	ext.textDir = file("$buildDir/text")
	ext.wavDir = file("$buildDir/wav")
	inputs.files pyFile, flacFile
	outputs.dirs textDir, wavDir
	commandLine "python", pyFile, process.metadataFile, flacFile
	doFirst {
		textDir.mkdirs()
		wavDir.mkdirs()
	}
}

convertTextToMaryXml {
	dependsOn extract
}

task packageData(type: Zip) {
	dependsOn convertTextGridToXLab
	from extract.wavDir, {
		into 'wav'
	}
	from extract.textDir, {
		into 'text'
	}
	from convertTextGridToXLab.destDir, {
		into 'lab'
	}
}

artifacts {
	'default' packageData
}
